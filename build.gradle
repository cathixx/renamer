plugins {
    id 'application'
    id 'idea'
    id 'eclipse'
    id 'io.freefair.lombok' version '6.4.3'
    id 'org.openjfx.javafxplugin' version '0.0.13'
}

group = 'de.cathixx'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

javafx {
    version = "19"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "repo"
    }
}

dependencies {
    implementation "de.cathixx:renamer-key:1.0.0"

    implementation "com.uwetrottmann.thetvdb-java:thetvdb-java:2.4.0"
    implementation "info.movito:themoviedbapi:1.10"
    implementation "org.slf4j:slf4j-api:2.0.5"

    testImplementation "org.junit.jupiter:junit-jupiter:5.9.1"

    runtimeOnly "org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0"
    runtimeOnly "org.apache.logging.log4j:log4j-api:2.19.0"
    runtimeOnly "org.apache.logging.log4j:log4j-core:2.19.0"

    // include javafx windows binaries to jar
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    // include javafx linux binaries to jar
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    // include javafx mac binaries to jar
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
}

test {
    useJUnitPlatform()
}

application {
    mainClassName = 'de.cathixx.renamer.Launcher'
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveFileName = 'renamer.jar'
    manifest {
        attributes "Main-Class": application.mainClassName
        attributes 'Implementation-Version': archiveVersion.get()
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

clean {
    delete 'out'
}
